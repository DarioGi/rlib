dnl Configure template for RLIB

dnl Copyright (c) 2003 SICOM Systems Inc (bdoan@sicompos.com)
dnl RLIB is free software; you can redistribute it and/or modify
dnl it under the terms of the GNU General Public License as published by
dnl the Free Software Foundation; either version 2, or (at your option)
dnl any later version.

dnl RLIB is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU General Public License for more details.

dnl You should have received a copy of the GNU General Public License
dnl along with this program; if not, write to the Free Software
dnl Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
dnl 02111-1307, USA.

dnl Report bugs to rlib-devel@lists.sourceforge.net
dnl
dnl Process this file with autoconf to produce a configure script.



AC_INIT(libsrc/rlib.h)
AM_INIT_AUTOMAKE(rlib, 1.1.4)
AM_MAINTAINER_MODE
AC_DISABLE_STATIC
RLIB_VERSION=1.1.4
AC_SUBST(RLIB_VERSION)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_MAKE_SET
AC_PROG_INSTALL

AC_LIBTOOL_DLOPEN
AC_LIBLTDL_CONVENIENCE
AC_PROG_LIBTOOL
AC_SUBST(INCLTDL)
AC_SUBST(LIBLTDL)

dnl Checks for libraries.
AC_CHECK_LIB(c, exit)
AC_CHECK_LIB(m, exp)

AC_PATH_PROG(PKG_CONFIG, pkg-config, no)

PKG_CHECK_MODULES(LIBXML, libxml-2.0 >= 2.4.0)
AC_SUBST(LIBXML_LIBS)
AC_SUBST(LIBXML_CFLAGS)

AC_ARG_ENABLE(mysql,              [  --disable-mysql         disable mysql backend],,enable_mysql=yes)
MYSQL_CFLAGS=""
MYSQL_LIBS=""
if test x$enable_mysql = xyes; then
	AC_PATH_PROG(MYSQL_CONFIG, mysql_config, no)
	if test x$MYSQL_CONFIG = xno; then
		echo "*** WARNING: MYSQL NOT FOUND IN PATH.  RLIB WILL NOT BE COMPILED WITH SUPPORT FOR IT"
		enable_mysql=no	
	else
		AC_DEFINE(HAVE_MYSQL,1,[Build with MYSQL support])
		MYSQL_CFLAGS=`$MYSQL_CONFIG --cflags`
		MYSQL_LIBS=`$MYSQL_CONFIG --libs`
	fi
fi
AC_SUBST(MYSQL_CFLAGS)
AC_SUBST(MYSQL_LIBS)
AM_CONDITIONAL(HAVE_MYSQL, test x$enable_mysql = xyes)

AC_ARG_ENABLE(postgre,              [  --disable-postgre       disable postgre backend],,enable_postgre=yes)
POSTGRE_CFLAGS=""
POSTGRE_LIBS=""
if test x$enable_postgre = xyes; then
	AC_PATH_PROG(POSTGRE_CONFIG, pg_config, no)
	if test x$POSTGRE_CONFIG = xno; then
		echo "*** WARNING: POSTGRE NOT FOUND IN PATH.  RLIB WILL NOT BE COMPILED WITH SUPPORT FOR IT"
		enable_postgre=no
	else
		AC_DEFINE(HAVE_POSTGRE,1,[Build with POSTGRE support])
		POSTGRE_CFLAGS="-I `$POSTGRE_CONFIG --includedir`"
		POSTGRE_LIBS="-L`$POSTGRE_CONFIG --libdir` -lpq"
	fi
fi
AC_SUBST(POSTGRE_CFLAGS)
AC_SUBST(POSTGRE_LIBS)
AM_CONDITIONAL(HAVE_POSTGRE, test x$enable_postgre = xyes)

dnl .. ODBC checking borrowed from libgda
ODBC_CFLAGS=""
ODBC_LIBS=""
enable_odbc=yes
AC_ARG_WITH(odbc,
[  --with-odbc=<directory>	use ODBC libraries in <directory>],[
if test $withval = no
then
	enable_odbc=false
elif test $withval = yes
then
	dir="/usr/local"
else
	dir=$withval
fi
])
odbcdir=""
if test $enable_odbc = yes
then
	AC_MSG_CHECKING(for ODBC files)
	for d in $dir /usr /usr/local
	do
		if test -f $d/lib/libodbc.so -a -f $d/include/sql.h
		then
			AC_MSG_RESULT(found ODBC in $d)
			odbclib="-lodbc"
			odbcdir=$d
			break
		fi
		if test -f $d/lib/libiodbc.so -a -f $d/include/sql.h
		then
			AC_MSG_RESULT(found iODBC in $d)
			odbclib="-liodbc"
			odbcdir=$d
			break
		fi
	done
	if test x$odbcdir = x
	then
		enable_odbc=no
		AC_MSG_WARN(ODBC input method is not going to be used)
	else
		AC_DEFINE(HAVE_ODBC,1,[Build with ODBC support])
		ODBC_CFLAGS="-I${odbcdir}/include"
		ODBC_LIBS="-L${odbcdir}/lib ${odbclib}"
	fi
fi
AM_CONDITIONAL(HAVE_ODBC, test x$enable_odbc = xyes)
AC_SUBST(ODBC_CFLAGS)
AC_SUBST(ODBC_LIBS)


AC_ARG_ENABLE(php,              [  --disable-php         disable php frontend],,enable_php=yes)
PHP_CFLAGS=""
PHP_EXTENSION_DIR=""
if test x$enable_php = xyes; then
	AC_PATH_PROG(PHP_CONFIG, php-config, no)
	if test x$PHP_CONFIG = xno; then
		echo "*** WARNING: PHP NOT FOUND IN PATH.  RLIB WILL NOT BE COMPILED WITH SUPPORT FOR IT"
		enable_php=no
	else
		AC_DEFINE(HAVE_PHP,1,[Build with PHP support])
		PHP_CFLAGS=`$PHP_CONFIG --includes`
		PHP_EXTENSION_DIR=`$PHP_CONFIG --extension-dir`
	fi
fi
AC_SUBST(PHP_CFLAGS)
AC_SUBST(PHP_EXTENSION_DIR)
AC_SUBST(HAVE_PHP)
AM_CONDITIONAL(HAVE_PHP, test x$enable_php = xyes)

AC_ARG_ENABLE(python,              [  --disable-python      disable python frontend],,enable_python=yes)
PYTHON_CFLAGS=""
PYTHON_EXTENSION_DIR=""
if test x$enable_python = xyes; then
	AM_PATH_PYTHON(2.2)
	if test x$PYTHON_VERSION = x2.2; then
		AC_DEFINE(HAVE_PYTHON,1,[Build with PYTHON support])
		PYTHON_CFLAGS="-I $PYTHON_PREFIX/include/python$PYTHON_VERSION"
		PYTHON_EXTENSION_DIR=$pyexecdir
	else
		echo "*** WARNING: PYTHON 2.2 NOT FOUND IN PATH.  RLIB WILL NOT BE COMPILED WITH SUPPORT FOR IT"
		enable_python=no
	fi
fi
AC_SUBST(HAVE_PYTHON)
AC_SUBST(PYTHON_CFLAGS)
AC_SUBST(PYTHON_EXTENSION_DIR)
AM_CONDITIONAL(HAVE_PYTHON, test x$enable_python = xyes)

RLIB_CFLAGS="$LIBXML_CFLAGS $MYSQL_CFLAGS $POSTGRE_CFLAGS $ODBC_CFLAGS $PHP_CFLAGS $PYTHON_CFLAGS -Wall -Werror"
RLIB_LIBS="$LIBXML_LIBS $MYSQL_LIBS $POSTGRE_LIBS $ODBC_LIBS -lcpdf"
AC_SUBST(RLIB_CFLAGS)
AC_SUBST(RLIB_LIBS)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h unistd.h)
AC_C_CONST

AC_CHECK_HEADER([cpdflib.h],
	[AC_DEFINE([HAVE_CPDFLIB_H])],
	[AC_MSG_ERROR([You must have cpdflib properly installed in order for rlib to work.. missing cpdflib.h])])
AC_CHECK_LIB(cpdf, cpdf_init, [], [AC_MSG_ERROR([You must have cpdflib properly installed in order for rlib to work.. missing libcpdf.so])])

AC_ARG_ENABLE(memdebug,              [  --enable-memdebug       enable memory debugging],enable_memdebug="$enableval",enable_memdebug=no)
if test x$enable_memdebug = xyes; then
	AC_DEFINE(DEBUG_RLIB_MEMORY,1,[Debug RLIB memory usage])
fi



AC_OUTPUT([
Makefile
rlib.pc
libsrc/Makefile
bindings/Makefile
bindings/php/Makefile
bindings/python/Makefile
inputs/Makefile
inputs/mysql/Makefile
inputs/odbc/Makefile
inputs/postgre/Makefile
tests/Makefile
])


echo "
rlib-$VERSION:

	prefix:                   ${prefix}
	compiler:                 ${CC}
	Memory debugging:         ${enable_memdebug}

	INPUTS:
	 MYSQL:                   ${enable_mysql}
	 POSTGRE:                 ${enable_postgre}
	 ODBC:                    ${enable_odbc}

	BINDINGS:
	 PHP:                     ${enable_php}
	  PHP entension dir:      ${PHP_EXTENSION_DIR}
	 PYTHON:                  ${enable_python}
	  PYTHON entension dir:   ${PYTHON_EXTENSION_DIR}

	WOCKET:                   In My Pocket
"
